 include "..\const\include.xys";

	if(isButtonDownCTRL()){
		$updateShowData = 1;
		msg "CTRL was held at start, will update all shows that needs updating!";
	}
	else{
		$updateShowData = 0;
	}
	$selectedPath = getFullPath();
	$selectedName = getFolderName();

	if(isTV($selectedPath) && $selectedPath){
		$tvShowsList = $selectedName;
		$updateSingleShow = 1;
		msg "Will only update: " . $selectedName;
	}
	else{
		$tvShowsList = listfolder($dsTV,, 6);
		$updateSingleShow = 0;
	}

	$runBackup = 0;
	foreach($tvShow, $tvShowsList){
		unset $localfiles, $dbfiles;
		$updateDb = 0;
		status "Scanning for new episodes/shows: " . $tvShow;
		wait 10;
		//Checks if folder (TV-show) exists in database
		if(getsectionlist($tvShow, $dbTV) == ""){
			status "Found new Show: " . $tvShow;
			$imdbID = input("Found new Show!", 
			"Enter IMDB-id / URL for: " . $tvShow);
			$imdbID = trimIMDB($imdbID);
			setDbTVIMDb($tvShow, $imdbID);
			$mazeid = getTVMaze($tvShow);
			if(!$mazeid){
				$mazeid = input("TVMaze Id could not be set!", 
				"Enter manually for: " . $tvShow);
			}
			setDbTVMaze($tvShow, $mazeid);
			$updateDb = 1; //To trigger setting of data to Db
		}
		$fullPath = $dsTV . $tvShow;
		//Checks for new episodes, compares to database
		$dbfiles = getDBTVFiles($tvShow);
		$localfiles = getLocalTVFiles($fullPath);
		$localfiles  = episodeSorter($localfiles);
		//Local episodes doesnt match with Database-episodes
		if($localfiles != $dbfiles){
			foreach($file, $localfiles, |){
				//strpos returns -1 if needle isn't found in haystack
				if(strpos($dbfiles , $file) == -1){
					status "Found new episode: " . $tvShow . ": ". $file, $hexGreen, "ready";
					addNewTVEpisode($file, $tvShow);
					$updateDb = 1;
				}
			}
		}
		//Check if update is needed for tv-show, and if
		//CTRL was pressed at script start
		if(!$updateDb && $updateShowData){
			$dbLastEpDate = getDbTVNextAiredDate($tvShow);
			if($dbLastEpDate == "No data for next episode!"){
				$datediff = 0;
			}
			else{
				$datediff = datediff($dbLastEpDate);
			}
			$dbStatus = getDbTVStatus($tvShow);
			if(($dbStatus == "Running") && ($datediff >= 0)){
					$updateDb = 1;
			}
		}
		if($updateDb || $updateSingleShow){
			//Updates all database data for tv-shows
			status "Updating databases with new Show-info.", $hexGreen, "ready";
			setDBTVFiles($tvShow);
			setDbTVGenre($tvShow, getGenre($tvShow));
			setDbTVYear($tvShow, getYear($tvShow));
			$lastLocalEpisode = getLastLocalEpisode($fullPath);
			setDbTVLocalEp($tvShow, $lastLocalEpisode);
			$showStatus = TVshowStatus($tvShow);
			setDbTVStatus($tvShow, $showStatus);
			
			$nextAiredEpisode = getNextAiredEpisode($tvShow);
			$nextAiredEpisodeDate = getNextAiredDate($tvShow);
			$lastAiredEpisode = getLastAiredEpisode($tvShow);
			$lastAiredEpisodeDate = getLastAiredDate($tvShow);
			
			setDbTVLastAired($tvShow, $lastAiredEpisode);
			setDbTVLastAiredDate($tvShow, $lastAiredEpisodeDate);
			setDbTVNextAired($tvShow, getNextAiredEpisode($tvShow));
			setDbTVNextAiredDate($tvShow, $nextAiredEpisodeDate);
			$runBackup = 1;
		}
	}
	//Moves new.txt to ds/info and backups .db files
	if($runBackup){
		backupDBfiles();
	}
	status "Done!";
	releaseglobals 3;
	end 1;



