"_Db"
	//Faster than to include other libs
	perm $dbFilm = "C:\db\film.db";
	perm $dbTV = "C:\db\tv.db";
	perm $regexTVpath = "^\\\\DS\\TV\\";
	perm $regexMoviePath = "^\\\\DS\\FILM\\";
	perm $urlTxt = "D:\Google Drive\Scriptstuff\xyplorer\txt\url.txt";
	perm $skipList = "D:\Google Drive\Scriptstuff\xyplorer\txt\skiplist.txt";

	function isTV($path){
		return (regexmatches($path, $regexTVpath));
	}

	function isMovie($path){
		return (regexmatches($path, $regexMoviePath));
	}

"_TV : EpisodeCheck"
	sub "_Db";
	$localEp = getkey("localep", <cc_name>, $dbTV);
	$lastAired = getkey("lastaired", <cc_name>, $dbTV);
	$skipCheck = strpos(readfile($skipList) , <cc_name>);
	//American Dad has other season ordering on TVMaze, than releases
	if(<cc_name> == "American Dad"){
		$localEp = replace($localEp, "S14E", "S13E");
	}

    $retval = ($lastAired == $localEp || $skipCheck > -1) ?
        "Done: " . $localEp :
        "** Last: " . $lastAired . " [Local: " . $localEp . "]";
    return $retval;

"_TV : Year"
	sub "_Db";
    $year = isTV(<cc_item>) ?
        getkey("year", <cc_name>, $dbTV) :
        getkey("year", <cc_name>, $dbFilm);
	return $year;

"_MovieSubs : Subs"
	sub "_Db";
	$enSub = quicksearch("*en.srt", <cc_item>, |) ? 2 : 0;
	$svSub = quicksearch("*sv.srt", <cc_item>, |) ? 1 : 0;
	$subTot = $enSub + $svSub;
    $subStr = "";

    switch ($subTot) {
        case 1:
            $subStr = "Sv";
            break;
        case 2:
            $subStr = "En";
            break;
        case 3:
            $subStr = "En Sv";
            break;
    }

	return $subStr;

"_TV : Status"
	sub "_Db";
	$status = getkey("status", <cc_name>, $dbTV);
    return $status;

"_TV : Genre"
    sub "_Db";
    $genre = isTV(<cc_item>) ?
        getkey("genre", <cc_name>, $dbTV) :
        getkey("genre", <cc_name>, $dbFilm);
    return $genre;


"_TV : Title"
    sub "_Db";
    $title = isMovie(<cc_item>) ?
        getkey("Title", <cc_name>, $dbTV) :
        "";
    return $title;

"_LINKS : Links"
	sub "_Db";
	$path = <cc_path>;
	$name =  <cc_name>;
	$tvPopList = "RARBG;rarbg|TVMaze;tvmaze|IMDb;imdb";
	//Loads file with links to various websites
	$urls = readfile($urlTxt);
	if(isTV(<cc_path>)){
		$imdb = getkey("imdb", <cc_name>, $dbTV);
		$mazeid = getkey("tvmaze", <cc_name>, $dbTV);
		$choice = popupnested($tvPopList);
		//Find the token for url in <crlf>-separated list that contains value from popuplist
		$token = gettokenindex($choice, $urls, <crlf>);
		//Gets the url with the found token
		$site = gettoken($urls, $token, <crlf>);
		//Sets value to replace %STRING% with (in url-file)
		$replace = $choice == "tvmaze" ? $mazeid : $imdb;
		$site = replace($site, "%STRING%", $replace);
		//Opens the URL if the variable is set
		if(isset($site)){
			goto $site;
		}
	}
	elseif(isMovie(<cc_path>)){
		msg "MOVIE NOT BUILT!";
	}
