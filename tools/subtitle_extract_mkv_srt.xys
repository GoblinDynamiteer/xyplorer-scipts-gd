
    /* XYPlorer script
    Extract srt from selected mkv-file(s) and rename according to language.
    Uses MKVToolnix
    */

    include_once "..\const\include.xys";

    $srt_min_filesize = 5000;
    $srt_word_treshold = 200;
    $srt_word_hi_treshold = 20;

    $mkv_files = get("SelectedItemsPathNames", "|");

    foreach($mkv_file, $mkv_files){

        $mkv_file_path = gpc($mkv_file, "path"); //path without filename
        $mkv_file_name = gpc($mkv_file, "base"); //only filename, without extension

        $mkv_info = APP_MkvInfo($mkv_file);

        $track_numbers = regexmatches($mkv_info, $REGEX_SUBTITLE_MKV_INFO_NUMBERS, "|");
        $track_types = regexmatches($mkv_info, $REGEX_SUBTITLE_MKV_INFO_TYPES, "|");

        /*  List subtitle tracks for extraction */
        $track_extraction = "";
        $track_id = 0;
        foreach($track, $track_types){
            if(regexmatches($track, "subtitles")){
                $track_extraction = $track_extraction . "|" . $track_id;
            }
            $track_id++;
        }

        $track_extraction = LIST_Sort($track_extraction);

        /*  If mkv did not contain any subtitle tracks */
        if(!$track_extraction){
            status "No subtitles found in: " . $mkv_file;
            continue;
        }

        APP_ExtractSubtitle($mkv_file, $track_extraction);

        /*  List extracted srt-files, excluding already existing */
        $srt_files = listfolder(
            $mkv_file_path,
            "*" . $mkv_file_name . "_*.srt"
        );

        /*  Iterate trough srt-files */
        foreach($srt_file, $srt_files){
            /*  Delete small srt-files, probably forced for non-english parts */
            if(filesize($srt_file) < $srt_min_filesize){
                deletefile $srt_file;
                continue;
            }

            /*  Rename or delete files */
            if(SUB_Points($srt_file, "en") > $srt_word_treshold &&
                !exists($mkv_file_path . "\" . $mkv_file_name . $STRING_SUB_ENG_SUFFIX)){
                    if(SUB_Points($srt_file, "hi") > $srt_word_hi_treshold){
                        /*  Todo: Remove HI */
                        tag "HEARING IMPAIRED", $srt_file, $TAG_COMMENT;
                    }
                    renameitem(
                        $mkv_file_name . $STRING_SUB_ENG_SUFFIX,
                        $srt_file
                    );
            }
            elseif(SUB_Points($srt_file, "sv") > $srt_word_treshold &&
                !exists($mkv_file_path . "\" . $mkv_file_name . $STRING_SUB_SWE_SUFFIX)){
                    renameitem(
                        $mkv_file_name . $STRING_SUB_SWE_SUFFIX,
                        $srt_file
                    );
            }
            /*  SRT is neither eng/swe or exists already, delete  */
            else {
                deletefile $srt_file;
            }
        }
    }
