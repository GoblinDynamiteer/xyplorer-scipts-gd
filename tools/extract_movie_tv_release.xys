    include_once "..\const\include.xys";

    $path = SELECT_FullPath();
    $folder_name = SELECT_FolderName();

    /* If selected folder is a tv-release */
    if(CHECK_IsTvRelease($folder_name))
    {
        /* Determine extract tv-location */
        $tv_show_name_guess = STRING_TvNameFromRelease($folder_name);
        $tv_show_season_guess = STRING_TvSeasonFromRelease($folder_name);
        $extract_path_guess =
            $PATH_DS_TV . $tv_show_name_guess . "\" . $tv_show_season_guess;

        /* If path exists */
        $extract_destination = exists($extract_path_guess) ? $extract_path_guess
            : $PATH_DS_TV;

        /* Let user select destination */
        $destination = inputfolder(
            $extract_destination, "Select destination!");

        /* Move subs */
        /* Search for Subs, list only folders */
        $sub_folders = quicksearch("Subs /d", $path);

        foreach($folder, $sub_folders, $LINE_BREAK)
        {
            moveas $folder_name, $PATH_DS_SUBTITLES, $folder;
        }

        APP_Unrar($path, $destination);
        tag "Extracted: Delete Me!", $path, 2, 1;
        tag "Red", $path, 0, 0;
    }

    else // Movie release
    {
        /* Letter to extract release to */
        $letter = STRING_Moviefirst_letter($folder_name);

        /* Extract destination */
        $destination = $PATH_DS_MOVIE . $letter . "\" .  $folder_name;

        $confirmation = "Extract "  . $LINE_BREAK .
            $folder_name . " to "  . $LINE_BREAK . $destination;

        /* If user confirms extract location */
        if(confirm($confirmation, , 2, 4))
        {
            if(SUB_PathHasSubFolder($path))
            {
                moveas $folder_name, $PATH_DS_SUBTITLES, $path . "\Subs";
            }

            /* Tag folder for deletion */
            tag "Extracted: Delete Me!", $path, 2, 1;
            tag "Red", $path, 0, 0;

            /* Find rar-file */
            $rar_file = quicksearch("*.rar", $path, "|");

            while(gettoken($rar_file, "count", "|") != 1)
            {
                wait 100;
                $rar_file = quicksearch("*.rar", $path, "|");
                status "Waiting for subs move";
            }

            APP_Extract($rar_file, $destination);
            wait 100;
        }

        else // User cancels extraction
        {
            status "Cancelled", $STATUS_COLOR_ORANGE, stop;
        }
    }

    releaseglobals 3;
    end 1;
