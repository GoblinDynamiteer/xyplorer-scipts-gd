    include_once "..\const\include.xys";

    $path = SELECT_FullPath();

    if($path == $FUNCTION_RETURN_ERROR)
    {
        end 1;
    }

    $folder_name = SELECT_FolderName();

    /* If selected folder is a tv-release (full season or single episode) */
    if(CHECK_IsTvRelease($folder_name) ||
        CHECK_IsTvEpisodeRelease($folder_name))
    {
        /* Determine extract tv-location */
        $tv_show_name_guess = STRING_TvNameFromRelease($folder_name);
        $tv_show_season_guess = STRING_TvSeasonFromRelease($folder_name);
        $extract_path_guess =
            $PATH_DS_TV . $tv_show_name_guess . "\" . $tv_show_season_guess;

        /* If path exists */
        $extract_destination = exists($extract_path_guess) ?
            $extract_path_guess : $PATH_DS_TV;

        /* Let user select destination */
        $destination = inputfolder(
            $extract_destination, "Select destination!");

        /* Move subs */
        /* Search for Subs, list only folders ( /d )*/
        $sub_folders = quicksearch("Subs /d", $path);

        /* Subs are found */
        if($sub_folders)
        {
            foreach($folder, $sub_folders, $LINE_BREAK)
            {
                moveas $folder_name, $PATH_DS_SUBTITLES, $folder;
            }
        }

        APP_Unrar($path, $destination);

        /* Comment and color tag folder */
        tag "Extracted: Delete Me!", $path, 2, 1;
        tag "Red", $path, 0, 0;
    }

    else // Movie release
    {
        /* Letter to extract release to */
        $letter = STRING_Moviefirst_letter($folder_name);

        /* Get IMDB-id from nfo-file, if available */
        $nfo_file = FILE_ListFilesWithExtension($path, "nfo");
        $nfo_content = readfile($nfo_file);
        $imdb = STRING_TrimImdb($nfo_content);

        /* Extract destination */
        $destination = $PATH_DS_MOVIE . $letter . "\" .  $folder_name;

        $confirmation = "Extract "  . $LINE_BREAK .
            $folder_name . " to "  . $LINE_BREAK . $destination;

        /* If user confirms extract location */
        if(confirm($confirmation, , 2, 4))
        {
            if(SUB_PathHasSubFolder($path))
            {
                moveas $folder_name, $PATH_DS_SUBTITLES, $path . "\Subs";
            }

            /* Comment and color tag folder */
            tag "Extracted: Delete Me!", $path, 2, 1;
            tag "Red", $path, 0, 0;

            /* Find rar-file */
            $rar_file = quicksearch("*.rar", $path, "|");

            while(gettoken($rar_file, "count", "|") != 1)
            {
                wait 100;
                $rar_file = quicksearch("*.rar", $path, "|");
                status "Waiting for subs move";
            }

            APP_Extract($rar_file, $destination);

            /* Generate movie.nfo if imdb-id found */
            if($imdb)
            {
                FILE_GenerateMovieNfo($destination, $imdb);
            }
        }

        else // User cancels extraction
        {
            status "Cancelled", $STATUS_COLOR_ORANGE, stop;
        }
    }

    releaseglobals 3;
    end 1;
