    include_once "..\const\include.xys";

    $mouse_pointer_item = itematpos();

    $menu_tv = exists($PATH_DS_TV) ?
        string_generate_popup_menu_item(
            "TV: Last Season folder",
            "tv_last",
            $ICON_TV_ALT,
            0) : $NULL;

    $menu_script = string_generate_popup_menu_item(
                "Help on scripting command",
                "script_help",
                $ICON_HELP,
                0);

    $menu_app = string_generate_popup_menu_item(
                "App",
                "run_app",
                $ICON_APPLICATION,
                0);

    $menu_clipboard = string_generate_popup_menu_item(
                "ToClipBoard",
                "clipboard",
                $ICON_CLIPBOARD,
                0);

    $menu_locs = string_generate_popup_menu_item(
                "Locations",
                "goto_loc",
                $ICON_FOLDER,
                0);

    $menu_locs_sub_code = string_generate_popup_menu_item(
                "Code",
                "list_code_folders",
                $ICON_FOLDER,
                1);

    $menu_app_sub_putty = string_generate_popup_menu_item(
                "Putty",
                "open_app_putty",
                $ICON_TERMINAL,
                1);

    $menu_app_sub_ssh_pi = string_generate_popup_menu_item(
                "Putty SSH: Pi3",
                "open_app_putty_ssh_pi",
                $ICON_TERMINAL,
                1);

    $menu_clipboard_sub_api = string_generate_popup_menu_item(
                "API",
                "api",
                $ICON_CLIPBOARD,
                1);

    $menu_clipboard_sub_api_omdb = string_generate_popup_menu_item(
                "OMDb",
                "clipboard_api_omdb",
                $ICON_CLIPBOARD,
                2);

    $menu_atom = exists($mouse_pointer_item) == 2 ?
        string_generate_popup_menu_item(
            "#Atom [ ". $mouse_pointer_item ." ]",
            "open_in_atom",
            $ICON_ATOM,
            0) : $NULL;

    $menu_git_bash = exists($mouse_pointer_item) == 2 ?
        string_generate_popup_menu_item(
            "#GitBash [ ". $mouse_pointer_item . " ] ",
            "open_in_gitbash",
            $ICON_GIT,
            0) : $NULL;

    $menu_filezilla = exists($mouse_pointer_item) == 2 ?
        string_generate_popup_menu_item(
            "#FileZilla [ ". $mouse_pointer_item ." ]",
            "open_in_filezilla",
            $ICON_FILEZILLA,
            0) : $NULL;

    $menu_gitkraken = exists($mouse_pointer_item . "\.git") == 2 ?
        string_generate_popup_menu_item(
            "#GitKraken [ ". $mouse_pointer_item ." ]",
            "open_in_kraken",
            $ICON_KRAKEN,
            0) : $NULL;

    $menu_mkv_extract_subs = gpc($mouse_pointer_item, "ext") == "mkv" ?
        string_generate_popup_menu_item(
            "*Extract SRTs from [ ". gpc($mouse_pointer_item, "file") ." ]",
            "mkv_extract_subs",
            $ICON_KRAKEN,
            0) : $NULL;

    /* Generate menu */
    $menu =
    $menu_filezilla .   // Conditional: folder under cursor position
    $menu_git_bash .    // Conditional: folder under cursor position
    $menu_gitkraken .   // Conditional: folder under cursor position
    $menu_atom .        // Conditional: folder under cursor position
    $menu_mkv_extract_subs . // Conditional: mkv-file under cursor position
        ((  $menu_filezilla != $NULL ||
            $menu_atom != $NULL ||
            $menu_gitkraken != $NULL ||
            $menu_git_bash != $NULL) ||
            $menu_mkv_extract_subs != $NULL ?
                "-|" : "") .
    $menu_app .
        $menu_app_sub_putty .
        $menu_app_sub_ssh_pi .
    $menu_locs .
        $menu_locs_sub_code .
    $menu_tv .
    $menu_script .
    $menu_clipboard .
        $menu_clipboard_sub_api .
            $menu_clipboard_sub_api_omdb;

    $choice = popupnested($menu);

    /*  End if no choice is selected */
    if(!$choice){
        end 1;
    }

    switch($choice){
        case "open_in_atom":
            app_text_open_in_atom($mouse_pointer_item);
            break;

        case "open_in_filezilla":
            app_ftp_open_in_filezilla($mouse_pointer_item);
            break;

        case "open_in_kraken":
            app_git_openrepo_kraken($mouse_pointer_item);
            break;

        case "open_in_gitbash":
            app_git_openrepo_bash($mouse_pointer_item);
            break;

        case "mkv_extract_subs":
            selectitems($mouse_pointer_item);
            load "subtitle_extract_mkv_srt.xys";
            break;

        case "tv_last":
            $tv_show = inputselect(
                            "TV-Shows",
                            file_list_all_tv_show_folders(),
                            $NOT_USED,
                            72,
                            $NOT_USED,
                            "500",
                            "1000"
                        );
            $season = file_tv_get_last_season_in_path($PATH_DS_TV . $tv_show);
            goto $PATH_DS_TV . $tv_show . "\"  . $season;;
            break;

        case "script_help":
            $script_list = readfile($PATH_XYPLORER_HELP_SCRIPTING_TXT);
            $script_list = list_sort($script_list);
            $script_choice = inputselect(
                                "Commands",
                                $script_list,
                                $NOT_USED,
                                72,
                                $NOT_USED,
                                "500",
                                "1000"
                            );
            rtfm "idh_scripting_comref.htm#idh_sc_" . $script_choice;
            break;

        case "open_app_putty":
            open $exe_path_putty;
            break;

        case "open_app_putty_ssh_pi":
            app_ssh_putty(readfile($PATH_UNC_GD_DNS_NAME), "pi", "1122");
            break;

        case "clipboard_api_omdb":
            copytext readfile($PATH_OMDB_API_KEY);
            status "Copied OMDb API-key to clipboard!"
            break;

        case "list_code_folders":
            $item_arduino_builds = "List Arduino builds";
            $item_code_folder = "Kod";

            $choice = list_generate_user_selection_list(
                "Goto code folder",
                $item_arduino_builds ."|" .
                $item_code_folder);

            switch ($choice)
            {
                case $item_arduino_builds:
                    focus "P1";
                    goto $PATH_AND_FILTER_ARDUINO_BUILDS;
                    break;

                case $item_code_folder:
                    focus "P1";
                    goto "d:\kod";
                    break;

                default:
                    break;
            }

            break;

    }
