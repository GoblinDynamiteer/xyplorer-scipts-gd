    include_once "..\const\include.xys";

    $mouse_pointer_item = itematpos();

    $menu_tv = exists($PATH_DS_TV) ?
        STRING_GeneratePopupMenuItem(
            "TV: Last Season folder",
            "tv_last",
            $ICON_TV_ALT,
            0) : $NULL;

    $menu_script = STRING_GeneratePopupMenuItem(
                "Help on scripting command",
                "script_help",
                $ICON_HELP,
                0);

    $menu_app = STRING_GeneratePopupMenuItem(
                "App",
                "run_app",
                $ICON_APPLICATION,
                0);

    $menu_app_sub_putty = STRING_GeneratePopupMenuItem(
                "Putty",
                "open_app_putty",
                $ICON_TERMINAL,
                1);

    $menu_app_sub_ssh_pi = STRING_GeneratePopupMenuItem(
                "Putty SSH: Pi3",
                "open_app_putty_ssh_pi",
                $ICON_TERMINAL,
                1);

    $menu_atom = exists($mouse_pointer_item) == 2 ?
        STRING_GeneratePopupMenuItem(
            "#Atom [ ". $mouse_pointer_item ." ]",
            "open_in_atom",
            $ICON_ATOM,
            0) : $NULL;

    $menu_git_bash = exists($mouse_pointer_item) == 2 ?
        STRING_GeneratePopupMenuItem(
            "#GitBash [ ". $mouse_pointer_item . " ] ",
            "open_in_gitbash",
            $ICON_GIT,
            0) : $NULL;

    $menu_filezilla = exists($mouse_pointer_item) == 2 ?
        STRING_GeneratePopupMenuItem(
            "#FileZilla [ ". $mouse_pointer_item ." ]",
            "open_in_filezilla",
            $ICON_FILEZILLA,
            0) : $NULL;

    $menu_gitkraken = exists($mouse_pointer_item . "\.git") == 2 ?
        STRING_GeneratePopupMenuItem(
            "#GitKraken [ ". $mouse_pointer_item ." ]",
            "open_in_kraken",
            $ICON_KRAKEN,
            0) : $NULL;

    /* Generate menu */
    $menu =
    $menu_filezilla .   // Conditional: folder under cursor position
    $menu_git_bash .    // Conditional: folder under cursor position
    $menu_gitkraken .   // Conditional: folder under cursor position
    $menu_atom .        // Conditional: folder under cursor position
        ((  $menu_filezilla != $NULL ||
            $menu_atom != $NULL ||
            $menu_gitkraken != $NULL ||
            $menu_git_bash != $NULL) ?
                "-|" : "") .
    $menu_app .
        $menu_app_sub_putty .
        $menu_app_sub_ssh_pi .
    $menu_tv .
    $menu_script;

    $choice = popupnested($menu);

    /*  End if no choice is selected */
    if(!$choice){
        end 1;
    }

    switch($choice){
        case "open_in_atom":
            APP_Atom($mouse_pointer_item);
            break;

        case "open_in_filezilla":
            APP_FileZilla($mouse_pointer_item);
            break;

        case "open_in_kraken":
            APP_GitKraken($mouse_pointer_item);
            break;

        case "open_in_gitbash":
            APP_GitBash($mouse_pointer_item);
            break;

        case "tv_last":
            $tv_show = inputselect(
                            "TV-Shows",
                            FILE_ListTvFolders(),
                            $NOT_USED,
                            72,
                            $NOT_USED,
                            "500",
                            "1000"
                        );
            $season = FILE_TV_GetLastSeason($PATH_DS_TV . $tv_show);
            goto $PATH_DS_TV . $tv_show . "\"  . $season;;
            break;

        case "script_help":
            $script_list = readfile($PATH_XYPLORER_HELP_SCRIPTING_TXT);
            $script_list = LIST_Sort($script_list);
            $script_choice = inputselect(
                                "Commands",
                                $script_list,
                                $NOT_USED,
                                72,
                                $NOT_USED,
                                "500",
                                "1000"
                            );
            rtfm "idh_scripting_comref.htm#idh_sc_" . $script_choice;
            break;

        case "open_app_putty":
            open $exe_path_putty;
            break;

        case "open_app_putty_ssh_pi":
            APP_Putty(readfile($PATH_UNC_GD_DNS_NAME), "pi", "1122");
            break;

        default:
            break;
    }
