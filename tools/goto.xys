    include_once "..\const\include.xys";

    $mouse_pointer_item = itematpos();

	$tv_last_season_popup = "TV: Last Season folder;tv_last;" . $ICON_TV_ALT . "|";
	$script_popup = "Help on scripting command;script_help|";
    $open_app = "App;run_app;" . $ICON_APPLICATION . "|";
    $open_app_putty = " Putty;open_app_putty;" . $ICON_TERMINAL . "|";
    $open_app_putty_ssh = " SSH: Pi3;open_app_putty_ssh_pi;" . $ICON_TERMINAL . "|";
    $atom_popup = exists($mouse_pointer_item) == 2 ?
        "Open [ ". $mouse_pointer_item ." ] in Atom;open_in_atom;" .
        $ICON_ATOM . "|-|" : $NULL;
    $gitkraken_popup = exists($mouse_pointer_item . "\.git") == 2 ?
        "Open [ ". $mouse_pointer_item ." ] in GitKraken;open_in_kraken;" .
        $ICON_KRAKEN . "|-|" : $NULL;


	$choice = popupnested(
        $atom_popup .
        $open_app . $open_app_putty . $open_app_putty_ssh .
        $gitkraken_popup .
        $tv_last_season_popup .
        $script_popup
    );

    /*  End if no choice is selected */
	if(!$choice){
		end 1;
	}

	switch($choice){
        case "open_in_atom":
            APP_Atom($mouse_pointer_item);
            break;

        case "open_in_kraken":
            APP_GitKraken($mouse_pointer_item);
            break;

		case "tv_last":
			$tv_show = inputselect(
                            "TV-Shows",
                            FILE_ListTvFolders(),
                            $NOT_USED,
                            72,
                            $NOT_USED,
                            "500",
                            "1000"
                        );
			$season = FILE_TV_GetLastSeason($PATH_DS_TV . $tv_show);
			goto $PATH_DS_TV . $tv_show . "\"  . $season;;
			break;

		case "script_help":
			$script_list = readfile($PATH_XYPLORER_HELP_SCRIPTING_TXT);
			$script_list = LIST_Sort($script_list);
			$script_choice = inputselect(
                                "Commands",
                                $script_list,
                                $NOT_USED,
                                72,
                                $NOT_USED,
                                "500",
                                "1000"
                            );
			rtfm "idh_scripting_comref.htm#idh_sc_" . $script_choice;
			break;

        case "open_app_putty":
            open $exe_path_putty;
            break;

        case "open_app_putty_ssh_pi":
            APP_Putty(readfile($PATH_UNC_GD_DNS_NAME), "pi", "1122");
            break;

		default:
			break;
	}
