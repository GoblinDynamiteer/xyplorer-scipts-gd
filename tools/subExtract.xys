
  /* XYPlorer script
    Extract srt from selected mkv-file(s) and rename according to language.
    Uses MKVToolnix
  */

  include_once "..\const\include.xys";

  $regex_mkv_info_numbers = "Track\snumber\:\s\d{1,2}";
  $regex_mkv_info_type = "Track\stype\:\s.{1,10}";
  $srt_eng_suffix = ".en.srt";
  $srt_swe_suffix = ".sv.srt";
  $srt_min_filesize = 5000;
  $srt_word_treshold = 200;
  $srt_word_hi_treshold = 20;

  $mkv_files = get("SelectedItemsPathNames", "|");

  foreach($mkv_file, $mkv_files){

    $mkv_file_path = gpc($mkv_file, "path"); //path without filename
    $mkv_file_name = gpc($mkv_file, "base"); //only filename, without extension

    $info =  APP_MkvInfo($mkv_file);

    $track_numbers = regexmatches($info, $regex_mkv_info_numbers, "|");
    $track_types = regexmatches($info, $regex_mkv_info_type, "|");
    $track_id = 0;

    $track_extraction = "";

    foreach($track, $track_types){
      if(regexmatches($track, "subtitles")){
        $track_extraction = $track_extraction . "|" . $track_id;
      }
      $track_id++;
    }

    $track_extraction = sortList($track_extraction);
    APP_ExtractSubtitle($mkv_file, $track_extraction);

    /*  List extracted srt-files */
    $srt_files = listfolder($mkv_file_path, "*" . $mkv_file_name . "*.srt");

    /*  Iterate trough srt-files */
    foreach($srt_file, $srt_files){

      /*  Delete small srt-files, probably forced for non-english parts */
      if(filesize($srt_file) < $srt_min_filesize){
        deletefile $srt_file;
        continue;
      }

      /*  Rename files */
      if(SUB_Points($srt_file, "en") > $srt_word_treshold){
        if(SUB_Points($srt_file, "hi") > $srt_word_hi_treshold){
            /*  Todo: Remove HI */
          tag "HEARING IMPAIRED", $srt_file, 2;
        }
        /*  Todo: Check for collision, if file already exists */
        renameitem($mkv_file_name . $srt_eng_suffix, $srt_file);
      }
      elseif(SUB_Points($srt_file, "sv") > $srt_word_treshold){
        /*  Todo: Check for collision, if file already exists */
        renameitem($mkv_file_name . $srt_swe_suffix, $srt_file);
      }
      /*  Srt is neither eng or swe, delete  */
      else{
        deletefile $srt_file;
      }
    }
  }
