    include_once "..\const\include.xys";

    /* XYPlorer script
    Extract and rename SRT subtitles
    */

    /* Get active pane path */
    $path = get("path", a);

    /* Find compressed files */
    $zipFiles = FILE_ListFilesWithExtension($path, "zip");
    $rarFiles = FILE_ListFilesWithExtension($path, "rar");
    $srtCurrentFiles = FILE_ListFilesWithExtension($path, "srt");
    $srtCurrentSv = SUB_PathHasSvSub($path);
    $srtCurrentEn = SUB_PathHasEnSub($path);

    /* Extract found archives */
    if($zipFiles) // If zip-files were found
    {
        foreach($file, $zipFiles)
        {
            APP_ExtractWait($file, $path);
            deletefile $file;
        }
    }

    if($rarFiles) // If rar-files were found
    {
        foreach($file, $rarFiles)
        {
            APP_ExtractWait($file, $path);
            deletefile $file;
        }
    }

    $srtFiles = FILE_ListFilesWithExtension($path, "srt");
    $movieFileName = FILE_ListFilesWithExtension($path, "mkv");
    $movieFileName = getpathcomponent($movieFileName, "file");
    $movieFileName = replace($movieFileName, ".mkv");

    /* Determine new files */
    foreach($file, $srtCurrentFiles)
    {
        $srtFiles = replace($srtFiles, $file); // Removes $file string from $srtFiles
    }

    /* Sort list of new srt-files, removes empty elements */
    $srtFiles = LIST_Sort($srtFiles);

    foreach($file, $srtFiles)
    {
        if(SUB_DetermineLanguage($file) == $SUBTITLE_LANGUAGE_SWEDISH)
        {
            if($srtCurrentSv) // If path already has swe subtitle
            {
                deletefile $file; // Delete the srt-file
            }

            else {
                renameitem($movieFileName . $STRING_SUB_SWE_SUFFIX , $file)
            }
        }

        elseif(SUB_DetermineLanguage($file) == $SUBTITLE_LANGUAGE_ENGLISH)
        {
            if($srtCurrentEn) // If path already has eng subtitle
            {
                deletefile $file; // Delete the srt-file
            }

            else {
                renameitem($movieFileName . $STRING_SUB_ENG_SUFFIX , $file)
            }
        }

    }
