    /*  Extracts archived srt-files for zip- and rar-files
        Determines language of extracted srt-files and
        renames them accordingly.

        Languages used:
        English -> filename.en.srt
        Swedish -> filename.sv.srt

        English subtitles with hearing impaired text are
        tagged with "HEARING IMPAIRED".
    */

    include_once "..\const\include.xys";

    /* Get active pane path */
    $path = get("path", a);

    /* Find compressed files */
    $zip_files = file_list_files_with_extension($path, "zip");
    $rar_files = file_list_files_with_extension($path, "rar");
    $srt_files_current = file_list_files_with_extension($path, "srt");
    $srt_files_sv_current = subtitle_path_has_sv_sub($path);
    $srt_files_en_current = subtitle_path_has_en_sub($path);

    /* Extract found archives */
    if($zip_files)
    {
        foreach($file, $zip_files)
        {
            app_extract_sevenzip($file, $path, $APP_UNRAR_WAIT);
            deletefile $file;
        }
    }

    if($rar_files)
    {
        foreach($file, $rar_files)
        {
            app_extraction_unrar($file, $path, $APP_UNRAR_WAIT);
            deletefile $file;
        }
    }

    $srt_files = file_list_files_with_extension($path, "srt");
    $movie_file_name = file_list_files_with_extension($path, "mkv");
    $movie_file_name = getpathcomponent($movie_file_name, "file");
    $movie_file_name = replace($movie_file_name, ".mkv");

    /* Determine new files */
    foreach($file, $srt_files_current)
    {
        $srt_files = replace($srt_files, $file);
    }

    /* Sort list of new srt-files, removes empty elements */
    $srt_files = list_sort($srt_files);

    foreach($file, $srt_files)
    {
        if(subtitle_get_language($file) == $SUBTITLE_LANGUAGE_SWEDISH)
        {
            if($srt_files_sv_current)
            {
                deletefile $file;
            }

            else
            {
                renameitem($movie_file_name . $STRING_SUB_SWE_SUFFIX , $file)
            }
        }

        elseif(subtitle_get_language($file) == $SUBTITLE_LANGUAGE_ENGLISH)
        {
            if($srt_files_en_current)
            {
                deletefile $file;
            }

            else
            {
                renameitem($movie_file_name . $STRING_SUB_ENG_SUFFIX , $file)
            }
        }

    }
